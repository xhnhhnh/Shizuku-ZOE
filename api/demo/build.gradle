plugins {
    id('com.android.application')
    id('dev.rikka.tools.refine')
}

android {
    namespace 'rikka.shizuku.demo'
    defaultConfig {
        applicationId "rikka.shizuku.demo"
        versionCode 1
        versionName api_version_name
        externalNativeBuild {
            cmake {
                abiFilters "armeabi-v7a", 'arm64-v8a', 'x86', 'x86_64'
                cppFlags "-std=c++17"
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_static'
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
        aidl true
    }
    signingConfigs {
        sign
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    packaging {
        jniLibs {
            useLegacyPackaging true
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'

    implementation project(':api') // required by Shizuku and Sui
    implementation project(':provider') // required by Shizuku only

    compileOnly project(':demo-hidden-api-stub')
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:6.1' // bypass hidden api restriction, https://github.com/LSPosed/AndroidHiddenApiBypass
}
